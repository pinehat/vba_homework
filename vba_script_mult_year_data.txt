Sub multi_year_stock_price_and_volume()

For Each ws In Worksheets

'Get number of rows of data
    Dim last_row As Double
    last_row = ws.Cells(Rows.Count, 1).End(xlUp).Row
    ' MsgBox (last_row)

    ' Define initial working variables
    Dim stock_volume_total As Double
    Dim summary_table_row As Integer
    
    Dim year_opening_price As Double
    Dim year_closing_price As Double
    Dim year_price_change As Double
    
    ' Assign titles to summary table
    ws.Range("I1").Value = "Ticker"
    ws.Range("J1").Value = "Yearly Change"
    ws.Range("J1").ColumnWidth = 12
    ws.Range("K1").Value = "Percent Change"
    ws.Range("K1").ColumnWidth = 14
    ws.Range("L1").Value = "Total Stock Volume"
    ws.Range("L1").ColumnWidth = 17
    
    ' Initialize row counter and opening stock price for first use in the following for loop
    summary_table_row = 2
    year_opening_price = ws.Cells(2, 3).Value

    'Loop through data and extract unique stock tickers and each one's total volume
    'and assign to summary table
    For i = 2 To last_row
    
        ' If we are on the last row of a ticker
        If ws.Cells(i + 1, 1).Value <> ws.Cells(i, 1).Value Then

            ' List ticker in summary table
            ws.Range("I" & summary_table_row).Value = ws.Cells(i, 1).Value
            
            ' Compute the year's price change
            year_closing_price = ws.Cells(i, 6).Value
            year_price_change = year_closing_price - year_opening_price
            ws.Range("J" & summary_table_row).Value = year_price_change
            
            ' Color the cell green for 0 or positive change; red for negative
            If year_price_change >= 0 Then
                ws.Range("J" & summary_table_row).Interior.ColorIndex = 4
            Else
                ws.Range("J" & summary_table_row).Interior.ColorIndex = 3
            End If
            
            ' Handle situations where open and/or close price is zero
            If year_opening_price = 0 Or year_closing_price = 0 Then
                If year_opening_price = 0 And year_closing_price = 0 Then
                    ws.Range("K" & summary_table_row).Value = 0
                ElseIf year_opening_price = 0 And year_closing_price > 0 Then
                    ws.Range("K" & summary_table_row).Value = "Opened at 0"
                ElseIf year_opening_price > 0 And year_closing_price = 0 Then
                    ws.Range("K" & summary_table_row).Value = -1
                End If
            Else
                ' Compute and assign percentage change column
                ws.Range("K" & summary_table_row).Value = ((year_closing_price - year_opening_price) / year_opening_price)
            End If
                
            ' Increment the total stock volume
            stock_volume_total = stock_volume_total + ws.Cells(i, 7).Value
            ws.Range("L" & summary_table_row).Value = stock_volume_total
                    
            ' Reset total stock volume and year opening price for next go-around
            stock_volume_total = 0
            year_opening_price = ws.Cells(i + 1, 3).Value
                    
            ' Increment summary table row counter
            summary_table_row = summary_table_row + 1
                
        Else
            
            ' Increment total stock volume
            stock_volume_total = stock_volume_total + ws.Cells(i, 7).Value
            
        End If
                
    Next i
    
    ' Format percentage change column in summary table into % format
    ws.Range("K2:K" & summary_table_row - 1).NumberFormat = "0.00%"
    
    ' Populate a second summary table with the Ticker and Value of:
    '  a) stock that rose the greatest percentage
    '  b) stock that sank the greatest percentage
    '  c) stock that had the greatest trading volume
    
    'Assign second summary table headers
    ws.Range("O1").ColumnWidth = 20
    ws.Range("O2").Value = "Greatest % Increase"
    ws.Range("O3").Value = "Greatest % Decrease"
    ws.Range("O4").Value = "Greatest Total Volume"
    ws.Range("P1").Value = "Ticker"
    ws.Range("Q1").Value = "Value"
    ws.Range("Q1").ColumnWidth = 12
    ws.Range("Q2:Q3").NumberFormat = "0.00%"
      
    ' Declare variables for use in populating second summary table
    Dim summary_range As Range
    Dim match_row As Double
    
    ' Get max and min year's percentage change
    Set summary_range = ws.Range("K2:K" & summary_table_row - 1)
    ws.Range("Q2").Value = Application.WorksheetFunction.Max(summary_range)
    ws.Range("Q3").Value = Application.WorksheetFunction.Min(summary_range)
    
    ' Get ticker for max year's change and assign to summary table
    match_row = Application.WorksheetFunction.Match(ws.Range("Q2").Value, ws.Range("K1:K" & summary_table_row - 1), 0)
    ws.Range("P2").Value = ws.Cells(match_row, 9).Value
    
    ' Get ticker for min year's change and assign to summary table
    match_row = Application.WorksheetFunction.Match(ws.Range("Q3").Value, ws.Range("K1:K" & summary_table_row - 1), 0)
    ws.Range("P3").Value = ws.Cells(match_row, 9).Value
        
    ' Get the value of the stock with the highest trading volume, and then find its ticker
    Set summary_range = ws.Range("L2:L" & summary_table_row - 1)
    ws.Range("Q4").Value = Application.WorksheetFunction.Max(summary_range)
    match_row = Application.WorksheetFunction.Match(ws.Range("Q4").Value, ws.Range("L1:L" & summary_table_row - 1), 0)
    ws.Range("P4").Value = ws.Cells(match_row, 9).Value
    
    Next ws

End Sub
